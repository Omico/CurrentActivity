// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0-beta7'
        classpath 'io.fabric.tools:gradle:1.23.0'
        classpath "io.realm:realm-gradle-plugin:3.7.2"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    COMPILE_SDK_VERSION = 26
    BUILD_TOOLS_VERSION = "26.0.2"

    app = [
            APPLICATION_ID    : "me.omico.currentactivity",
            MIN_SDK_VERSION   : 19,
            TARGET_SDK_VERSION: COMPILE_SDK_VERSION,
            VERSION_CODE      : getVersionCode(),
            VERSION_NAME      : getVersionName()
    ]

    library = [
            MIN_SDK_VERSION   : 19,
            TARGET_SDK_VERSION: COMPILE_SDK_VERSION,
            VERSION_CODE      : 1,
            VERSION_NAME      : "1.0.0"
    ]

    ANDROID_SUPPORT_LIBRARY_VERSION = "26.1.0"
    ANDROID_SUPPORT_CONSTRAINT_LAYOUT_VERSION = "1.0.2"
    OMICO_FLOAT_WINDOW_VERSION = "1.2.0"
    OMICO_SETUP_WIZARD_LIBRARY_VERSION = "0.1.0"
    FABRIC_CRASHLYTICS_SDK_VERSION = "2.6.8@aar"
}

static int getVersionCode() {
    return readCommand('git rev-list HEAD --count').toInteger()
}

static String getVersionName() {
    return readCommand('git describe --tags --dirty')
}

static def readCommand(String cmd) {
    return cmd.execute().text.trim()
}