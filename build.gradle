// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.1'
        classpath 'com.google.gms:google-services:3.1.0'
        classpath 'io.fabric.tools:gradle:1.25.1'
        classpath "io.realm:realm-gradle-plugin:3.7.2"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    COMPILE_SDK_VERSION = 27

    app = [
            APPLICATION_ID    : "me.omico.currentactivity",
            MIN_SDK_VERSION   : 21,
            TARGET_SDK_VERSION: COMPILE_SDK_VERSION,
            VERSION_CODE      : getVersionCode(),
            VERSION_NAME      : getVersionName()
    ]

    library = [
            MIN_SDK_VERSION   : 19
    ]

    ANDROID_SUPPORT_LIBRARY_VERSION = "27.0.2"
    ANDROID_SUPPORT_CONSTRAINT_LAYOUT_VERSION = "1.1.0"
    OMICO_FLOAT_WINDOW_VERSION = "1.2.0"
    OMICO_SETUP_WIZARD_LIBRARY_VERSION = "0.2.0"
    FIREBASE_CORE_VERSION = "15.0.0"
    FABRIC_CRASHLYTICS_SDK_VERSION = "2.9.0@aar"
}

static int getVersionCode() {
    return readCommand('git rev-list HEAD --count').toInteger()
}

static String getVersionName() {
    return readCommand('git describe --tags --dirty')
}

static def readCommand(String cmd) {
    return cmd.execute().text.trim()
}