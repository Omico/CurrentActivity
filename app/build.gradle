apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

android {
    compileSdkVersion COMPILE_SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION
    defaultConfig {
        applicationId app.APPLICATION_ID
        minSdkVersion app.MIN_SDK_VERSION
        targetSdkVersion app.TARGET_SDK_VERSION
        versionCode app.VERSION_CODE
        versionName app.VERSION_NAME
        addManifestPlaceholders([APP_NAME: "@string/app_name"])
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            addManifestPlaceholders([APP_NAME: "@string/app_name_debug"])
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (variant.buildType.name == 'release') {
                outputFileName = "CurrentActivity-${defaultConfig.versionName}.apk"
            }
        }
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:$ANDROID_SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:design:$ANDROID_SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:support-vector-drawable:$ANDROID_SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support.constraint:constraint-layout:$ANDROID_SUPPORT_CONSTRAINT_LAYOUT_VERSION"
    implementation "me.omico.support.widget:floatwindow:$OMICO_FLOAT_WINDOW_VERSION"
    implementation "me.omico.aosp-part:setup-wizard-lib:$OMICO_SETUP_WIZARD_LIBRARY_VERSION"

    implementation project(':library')

    // Crashlytics
    implementation("com.crashlytics.sdk.android:crashlytics:$FABRIC_CRASHLYTICS_SDK_VERSION") {
        transitive = true
    }
}
